/* version 0.1 du                                                           */
/*  dernire modification :                                                 */
/*         01 06 2015 (SD)                                                  */
/*  auteur :                                                                */
/*         SD Samuel Dom (sdeom@epadesa.fr)                                */


/* Pour fonctionner il faut inclure pralablement les boites  outils :     */
/*  epTypeOutils                                                            */
/*  epPlanOutils                                                            */
/*  epTexteOutils (pour epPlanOutils)                                       */

/* Procdures de l'environnement                                            */
proc,Fed_md=EP_ZON_EDDV

/* Liste des routines                                                       */
/*  AplPlan Applique le plan du mme tat descriptif de division en volumes */
/*           dont l'altitude est immdiatement suprieure ou infrieure    */
/*           celle du plan actuellement trac                               */
/*  DetFED(prc_fed) Dtermine le matricule correspondant  l'tat descrip- */
/*           tif associ au plan qui est trac                              */
/*  DetPlanInf(prc_plan) Dtermine le matricule correspondant au plan du   */
/*           mme tat descriptif de division en volumes dont l'altitude    */
/*           est immdiatement infrieur  celle du plan actuellement trac */
/*  DetPlanSup(prc_plan) Dtermine le matricule correspondant au plan du   */
/*           mme tat descriptif de division en volumes dont l'altitude    */
/*           est immdiatement suprieur  celle du plan actuellement trac */


/*--------------------------------------------------------------------------*/
/*                                ROUTINES                                  */
/*                         par ordre alphabtique                           */
/*--------------------------------------------------------------------------*/

rout,AplPlan(type)
/*  Applique le plan du mme tat descriptif de division en volumes dont    */
/*           l'altitude est immdiatement infrieure ou suprieure  celle  */
/*           du plan actuellement trac selon que type vaut "Inf" ou "Sup" */
/*    S'il n'y a pas de plan rpondant  ce critre, ne fait rien           */
/*  Dernire modification : 01 06 2015 (SD)                                 */
{
  var,mat
  mat=[EpTypeVide]
  if,"[type==`Inf`]#[type==`Sup`]"
  {
    run,DetPlanEdd([type],mat)
  }
  if,"mat><EpTypeVide"
  {
    plna,[mat]
  }
}

rout,DetFED(prc_fed)
/* Dtermine le matricule correspondant  l'tat descriptif associ au plan */
/*           qui est trac                                                  */
/*    L'indice en renvoy dans la procdure dont le nom est indiqu dans    */
/*           prc_fed                                                       */
/*  Dernire modification : 01 06 2015 (SD)                                 */
{
  var,fed
  
  fed=
  if,"defprc(`jtMatPlan`)"
  {
    fed=[[jtMatPlan]&r0:E]
  }
  
  if,"fed><``"
  {
    [prc_fed]=[fed]
  }
  else
  {
    [prc_fed]=[EpTypeVide]
  }
}

rout,DetPlanEdd(type,prc_plan)
/*  Dtermine le matricule correspondant au plan du mme tat descriptif de */
/*           division en volumes dont l'altitude est immdiatement infri-  */
/*           eure ou suprieure  celle du plan actuellement trac          */
/*    L'indice en renvoy dans la procdure dont le nom est indiqu dans    */
/*           prc_plan                                                      */
/*  Dernire modification : 01 06 2015 (SD)                                 */
{
  var,fed,test
  
  [prc_plan]=[EpTypeVide]
  
  run,DetFED(fed)
  if,"fed><EpTypeVide"
  {
    test=!test(&r0:e==[fed])
    run,DetPlan([type],test,[prc_plan])
  }
}

rout,TraPlan(type)
/*  Applique le plan du mme tat descriptif de division en volumes dont    */
/*           l'altitude est immdiatement infrieure ou suprieure  celle  */
/*           du plan actuellement trac selon que type vaut "Inf" ou "Sup" */
/*           en conservant les paramtres de vue                            */
/*    S'il n'y a pas de plan rpondant  ce critre, ne fait rien           */
/*  Dernire modification : 01 06 2015 (SD)                                 */
{
  var,x,y,l,h,g,ec
  var,mat
  mat=[EpTypeVide]
  
  x=[xmvue]
  y=[ymvue]
  l=[2*dlvue]
  h=[2*dhvue]
  g=[%PLA:G]
  ec=[%PLA:E]
  
  /* on simule une application de plan */
  if,"[type==`Inf`]#[type==`Sup`]"
  {
    run,DetPlanEdd([type],mat)
  }
  if,"mat><EpTypeVide"
  {
    %PLA:X=[[mat]:X]
    %PLA:Y=[[mat]:Y]
    %PLA:L=[[mat]:Cx]
    %PLA:H=[[mat]:Cy]
    runr,comp=[mat],type=K
    {
      [[rr]:][ex]
    }
    jtMatPlan=[mat]
  }
  
  %PLA:G=[g]
  %PLA:E=[ec]
  cvue,x=[x],y=[y],l=[l],h=[h]
}